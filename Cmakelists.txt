cmake_minimum_required(VERSION 3.10)
project(SeamCarving VERSION 1.0)

# C++ standard
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# ---- OpenCV ----
find_package(OpenCV REQUIRED)
include_directories(${OpenCV_INCLUDE_DIRS})

# ---- OpenGL + GLFW ----
find_package(OpenGL REQUIRED)
find_package(glfw3 3.3 REQUIRED)

# ---- ImGui paths ----
set(IMGUI_DIR ${CMAKE_SOURCE_DIR}/external/imgui)
set(STB_DIR   ${CMAKE_SOURCE_DIR}/external/stb)

# ---- Sources ----
add_executable(seam_carving
    main.cpp
    SeamCarver.cpp
    SeamCarver.h

    # core ImGui
    ${IMGUI_DIR}/imgui.cpp
    ${IMGUI_DIR}/imgui_draw.cpp
    ${IMGUI_DIR}/imgui_tables.cpp
    ${IMGUI_DIR}/imgui_widgets.cpp
    ${IMGUI_DIR}/imgui_demo.cpp

    # backends
    ${IMGUI_DIR}/backends/imgui_impl_glfw.cpp
    ${IMGUI_DIR}/backends/imgui_impl_opengl2.cpp
)

target_include_directories(seam_carving PRIVATE
    ${IMGUI_DIR}
    ${IMGUI_DIR}/backends
    ${STB_DIR}
)

# ---- Link libraries ----
target_link_libraries(seam_carving PRIVATE
    ${OpenCV_LIBS}
    OpenGL::GL
    glfw
)

# ---- Warnings ----
if(MSVC)
    target_compile_options(seam_carving PRIVATE /W4)
else()
    target_compile_options(seam_carving PRIVATE -Wall -Wextra -Wpedantic)
endif()

message(STATUS "OpenCV version: ${OpenCV_VERSION}")
message(STATUS "OpenCV include dir: ${OpenCV_INCLUDE_DIRS}")
message(STATUS "OpenCV libraries: ${OpenCV_LIBS}")
